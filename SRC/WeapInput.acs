#library "WeapInput"
#include "zcommon.acs"

script "CheckAttackButtons" ENTER
{
	int buttons;
	while (TRUE)
	{
		buttons = GetPlayerInput(-1, INPUT_BUTTONS);
			
		if (buttons & BT_ATTACK)
		{
			GiveInventory("FiredPrimary", 1);
		}
		else
			TakeInventory("FiredPrimary", 1);

		if (buttons & BT_ALTATTACK)
		{
			GiveInventory("FiredSecondary", 1);
		}
		else
			TakeInventory("FiredSecondary", 1);
			delay(2);
	}
} 
	
script "CheckWeaponAction" ENTER
{ 
    int buttons;
    
    while (TRUE)
    {
        buttons = GetPlayerInput(-1, INPUT_BUTTONS) & (~GetPlayerInput(-1, INPUT_OLDBUTTONS));

        if(buttons & BT_ZOOM)
        {
            GiveInventory("BDWeaponAction", 1);
            GiveInventory("Kicking", 1);
            GiveInventory("GoSpecial", 1);
            delay(1);
        }
        else if (buttons & BT_RELOAD)
        {
            GiveInventory("Reloading", 1);
            GiveInventory("BDWeaponAction", 1);
            GiveInventory("GoSpecial", 1);
            delay(1);
        }
		else if (buttons & BT_USER1)
        {
            GiveInventory("TossGrenade", 1);
            GiveInventory("BDWeaponAction", 1);
            GiveInventory("GoSpecial", 1);
            delay(1);
        }
        delay(1);
    }
}

Script "BDweaponActions" (int Type) NET
{
	if(CheckInventory("InAction") > 0) terminate;
	
    GiveInventory("BDWeaponAction", 1);
    GiveInventory("GoSpecial", 1);
	str token;
	int delaytic = 10;
    
	Switch(Type)
    {
		default:
			terminate;
			break;
        Case 0: token = "Reloading"; break;
        Case 1: token = "TossGrenade"; break;
    }
	GiveInventory("InAction", 1);
	GiveInventory(token, 1);
	delay(delaytic);
	TakeInventory(token, 1);
	TakeInventory("InAction", 1);
}