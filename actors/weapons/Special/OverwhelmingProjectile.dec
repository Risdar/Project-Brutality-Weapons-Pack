Actor HealStreamItem : Health
{
   Inventory.PickupSound ""
   Inventory.PickupMessage ""
   Inventory.Amount 4
   Inventory.MaxAmount 999999
   +INVENTORY.ALWAYSPICKUP
   -COUNTITEM
   -INVENTORY.PICKUPFLASH
   States
   {
   Spawn:
    TNT1 A -1
	Stop
   }
}

Actor HealStreamDamager : CustomInventory
{
   Inventory.Amount 1
   Inventory.MaxAmount 1
   +INVENTORY.ALWAYSPICKUP
   +INVENTORY.AUTOACTIVATE
   States
   {
   Spawn:
    TNT1 A -1
	Stop
   Pickup:
    TNT1 A 0 DamageThing(10,0)
	Stop
   }
}

//Missile  
ACTOR MissileExplode
{ 
    +NOINTERACTION
    States
	{
	Spawn:
		TNT1 A 0 NoDelay A_QuakeEx(3, 3, 3, 15, 0, 256, "none", QF_SCALEDOWN) 
		TNT1 A 0 A_SpawnItemEx("MissileExploder",
			frandom(13.0,-13.0), frandom(13.0,-13.0), random(13.0,-13.0),
			frandom(0.6,0.8), frandom(0.6,0.8), frandom(0.6,0.8),
			frandom(0.0,360.0), SXF_CLIENTSIDE, 0)
		TNT1 A 0 A_JumpIf(GetCVar("RO_SmokeChoker"),"NULL")
		TNT1 AA 0 A_SpawnItemEx("MissileExpSmoke",
			frandom(13.0,-13.0), frandom(13.0,-13.0), random(13.0,-13.0),
			frandom(1.0,0.5), frandom(1.0,0.5), frandom(1.0,0.5),
			frandom(0.0,360.0), SXF_CLIENTSIDE, 0)
		TNT1 AAAAA 5 A_SpawnItemEx("MissileExpSmoke",
			frandom(13.0,-13.0), frandom(13.0,-13.0), random(13.0,-13.0),
			frandom(1.0,0.5), frandom(1.0,0.5), frandom(1.0,0.5),
			frandom(0.0,360.0), SXF_CLIENTSIDE, 0)
		Stop
	}
}

ACTOR MissileExpSmoke
{
	+NOINTERACTION
	+ROLLSPRITE
	+ROLLCENTER
	Renderstyle "Translucent"
    Scale 0.65
    Alpha 0.0
	var float User_Rotation;
	var float User_Direction;
	var int User_AlphaSwitch;
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_SetScale(frandom(0.65,0.5))					//Set scale.
		Goto TwatRoll
	Twatroll:
		TNT1 A 0 A_SetRoll(frandom(0.0,360.0), SPF_INTERPOLATE)			//Randomized start.
		TNT1 A 0 A_SetUserVarFloat("User_Rotation", frandom(2.0,5.0))	//Randomized rotation speed.
		TNT1 A 0 A_SetUserVarFloat("User_Direction", frandom(1.0,-1.0))	//Sets the rotation direction.
		TNT1 A 0 A_Jump(256,"SmokeA","SmokeB","SmokeC","SmokeD",
							"SmokeE","SmokeF","SmokeG","SmokeH")
	SmokeA:
		SMKE A 0 A_Jump(256,"Main")
	SmokeB:
		SMKE B 0 A_Jump(256,"Main")
	SmokeC:
		SMKE C 0 A_Jump(256,"Main")
	SmokeD:
		SMKE D 0 A_Jump(256,"Main")
	SmokeE:
		SMKE E 0 A_Jump(256,"Main")
	SmokeF:
		SMKE F 0 A_Jump(256,"Main")
	SmokeG:
		SMKE G 0 A_Jump(256,"Main")
	SmokeH:
		SMKE H 0 A_Jump(256,"Main")
	Main:
		"####" "#" 1 {
			If(Alpha >= 0.3) {User_AlphaSwitch = 1; }
			If(User_AlphaSwitch) {
				A_FadeOut(0.0025+frandom(0.001, 0.006));
			} Else {
				A_FadeIn(frandom(0.03,0.05));
			}
			A_ChangeVelocity(frandom(0.1,-0.1),frandom(0.1,-0.1),frandom(0.1,-0.1),CVF_RELATIVE);
			A_SetScale(ScaleX + 0.005); 										 //Dissipate outward.
			A_ScaleVelocity(0.97); 												 //Slow down.
			A_SetRoll(Roll + (User_Rotation * User_Direction), SPF_INTERPOLATE); //Rotation.
			If(User_Rotation > 0.05) {											 //Rotation clamp.
				User_Rotation = User_Rotation * 0.95; 							 //Slow down the rotation.
			}
		}
		Loop
	}
}

ACTOR MissileExploder
{ 
    +NOINTERACTION
    States
    {
    Spawn:
		TNT1 AAA 2 A_SpawnItemEx("MissileExp",frandom(7.0,-7.0),frandom(7.0,-7.0),
			frandom(7.0,-7.0),0,0,0,0,SXF_CLIENTSIDE)
		Stop
    }
}

ACTOR MissileExp
{
	+NOINTERACTION
	Renderstyle "Add"
	states
	{
	Spawn:
		TNT1 A 0 NoDelay A_SetScale(frandom(1.5,1.8))
		TNT1 A 0 A_Jump(256,"Exp1","Exp2","Exp3","Exp4","Exp5")
	Exp1:
		EXP6 A 0 A_Jump(256,"Main")
	Exp2:
		EXP7 A 0 A_Jump(256,"Main")
	Exp3:
		EXP8 A 0 A_Jump(256,"Main")
	Exp4:
		EXP9 A 0 A_Jump(256,"Main")
	Exp5:
		EXP0 A 0 A_Jump(256,"Main")
	Main:
		"####" ABCDEFGH 2 Bright { A_SetScale(ScaleX - 0.01); Return A_JumpIf(ScaleX <= 0, "Null"); }
		Stop
	}
}

//Missile
actor NukerMissileExplode
{ 
    +NOINTERACTION
    States
    {
    Spawn:
		TNT1 A 15 NoDelay {
			A_QuakeEx(3, 3, 3, 15, 0, 384, "none", QF_SCALEDOWN);
			A_SpawnItemEx("NukerMissileFlash",0,0,0,0,0,0,0,SXF_CLIENTSIDE);
			A_SpawnItemEx("NukerMissileExploder",0,0,0,0,0,0,0,SXF_CLIENTSIDE);
			If(!GetCVar("RO_SmokeChoker")) { A_SpawnItemEx("NukerMissileSmoker",0,0,0,0,0,0,0,SXF_CLIENTSIDE); }
		}
		Stop
	}
}

actor NukerMissileFlash
{
    +NOINTERACTION
    Renderstyle "Add"
    Scale 0.5
    States
    {
    Spawn:
		FLAR A 1 Bright {
			A_SetScale(ScaleX + 0.05);
			A_FadeOut(0.125);
		}
		Loop
	}
}

actor NukerMissileExploder
{
    +NOINTERACTION
    States
    {
	Spawn:
		TNT1 AAAAA 1 NoDelay A_SpawnItemEx("NukerMissileExp",frandom(21.0,-21.0),frandom(21.0,-21.0),frandom(21.0,-21.0),0,0,0,0,SXF_CLIENTSIDE)
		Stop
    }
}

actor NukerMissileExp
{
	+NOINTERACTION
	Renderstyle "Add"
	Scale 0.4
	states
	{
	Spawn:
		TNT1 A 0 NoDelay A_Jump(256,"Sprite1","Sprite2","Sprite3","Sprite4")
	Sprite1:
		FLA1 A 0 A_Jump(256,"Main")
	Sprite2:
		FLA2 A 0 A_Jump(256,"Main")
	Sprite3:
		FLA3 A 0 A_Jump(256,"Main")
	Sprite4:
		FLA4 A 0 A_Jump(256,"Main")
	Main:	
		"####" "#" 1 Bright {
			A_SetScale(ScaleX - 0.01);
			A_FadeOut(0.05);
			Return A_JumpIf(ScaleX <= 0, "Null");
		}
		Loop
	}
}

actor NukerMissileSmoker
{
    +NOINTERACTION
	var int user_counter;
    States
    {
    Spawn:
		TNT1 A 8 {
			for(user_counter = 0; user_counter < 5; user_counter++) {
				A_SpawnItemEx("NukerMissileExpSmoke",frandom(26.0,-26.0),
					frandom(26.0,-26.0),frandom(26.0,-26.0),frandom(0.2,0.4),0,
					frandom(-0.3,0.3),frandom(0.0,360.0),SXF_CLIENTSIDE);
			}
		}
		TNT1 AAAA 1 A_SpawnItemEx("NukerMissileExpSmoke",frandom(26.0,-26.0),
			frandom(26.0,-26.0),frandom(26.0,-26.0),frandom(0.2,0.4),0,
			frandom(-0.3,0.3),frandom(0.0,360.0),SXF_CLIENTSIDE)
		Stop
    }
}

actor NukerMissileExpSmoke
{
	Renderstyle "Translucent"
	Alpha 0.0
	Scale 0.6
	+NOINTERACTION
	var float User_Rotation;
	var float User_Direction;
	var int User_AlphaSwitch;
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_SetScale(frandom(0.6,0.5))
		Goto TwatRoll
	Twatroll:
		TNT1 A 0 A_SetRoll(frandom(0.0,360.0), SPF_INTERPOLATE)			//Randomized start.
		TNT1 A 0 A_SetUserVarFloat("User_Rotation", frandom(2.0,5.0))	//Randomized rotation speed.
		TNT1 A 0 A_SetUserVarFloat("User_Direction", frandom(1.0,-1.0))	//Sets the rotation direction.
		TNT1 A 0 A_Jump(256,"SpriteA","SpriteB","SpriteC","SpriteD")
	SpriteA:
		NSMK A 0 A_Jump(256,"Main")
	SpriteB:
		NSMK B 0 A_Jump(256,"Main")
	SpriteC:
		NSMK C 0 A_Jump(256,"Main")
	SpriteD:
		NSMK D 0 A_Jump(256,"Main")
	Main:
		"####" "#" 1 {
			If(Alpha >= 0.3) {User_AlphaSwitch = 1; }
			If(User_AlphaSwitch) {
				A_FadeOut(0.0025+frandom(0.001, 0.006));
			} Else {
				A_FadeIn(frandom(0.03,0.05));
			}
			A_ChangeVelocity(frandom(0.1,-0.1),frandom(0.1,-0.1),frandom(0.1,-0.1),CVF_RELATIVE);
			A_SetScale(ScaleX +0.01);											 //Dissipate outward.
			A_ScaleVelocity(0.97); 												 //Slow down.
			A_SetRoll(Roll + (User_Rotation * User_Direction), SPF_INTERPOLATE); //Rotation.
			If(User_Rotation > 0.05) {											 //Rotation clamp.
				User_Rotation = User_Rotation * 0.95; 							 //Slow down the rotation.
			}
		}
		Loop
	}
}

//Larpa & Gauss
ACTOR PelletExplode
{ 
    +NOINTERACTION
    States
	{
    Spawn:
		TNT1 A 0 NoDelay A_QuakeEx(1,1,1,8,0,192,"none",QF_SCALEDOWN)
		TNT1 AA 0 A_SpawnItemEx("PelletExploder",0,0,0,
			frandom(1.3,2.0),frandom(1.3,2.0),frandom(1.3,2.0),
			frandom(0.0,360.0),SXF_CLIENTSIDE)
		TNT1 A 0 A_JumpIf(GetCVar("RO_SmokeChoker"),"Null")
		TNT1 AAA 2 A_SpawnItemEx("PelletExpSmoke",0,0,0,
			frandom(0.4,0.7),frandom(0.4,0.7),frandom(0.4,0.7),
			frandom(0.0,360.0),SXF_CLIENTSIDE)
		TNT1 A 2
		Stop
	}
}

ACTOR PelletExpSmoke : MissileExpSmoke
{ 
    Scale 0.4
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_SetScale(frandom(0.3,0.4))
		Goto Super::TwatRoll
	}
}

ACTOR PelletExploder
{ 
    +NOINTERACTION
    States
    {
    Spawn:
		TNT1 AAA 2 A_SpawnItemEx("PelletExp",
			frandom(4.0,3.0),frandom(4.0,3.0),frandom(4.0,3.0),
			0,0,0,0,SXF_CLIENTSIDE,0)
		Stop
    }
}

ACTOR PelletExp : MissileExp
{
	Scale 0.85
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_SetScale(frandom(0.75,0.95))
		TNT1 A 0 A_Jump(256,"Exp1","Exp2","Exp3","Exp4","Exp5")
		Goto Spawn
	}
}

//Larpa & Gauss
actor NukerPelletExplode
{ 
    +NOINTERACTION
    States
    {
    Spawn:
		TNT1 A 8 NoDelay {
			A_QuakeEx(1, 1, 1, 8, 0, 288, "none", QF_SCALEDOWN);
			A_SpawnItemEx("NukerPelletFlash",0,0,0,0,0,0,0,SXF_CLIENTSIDE);
			A_SpawnItemEx("NukerPelletExploder",0,0,0,0,0,0,0,SXF_CLIENTSIDE);
			If(!GetCVar("RO_SmokeChoker")) {
				A_SpawnItemEx("NukerPelletSmoker",0,0,0,0,0,0,0,SXF_CLIENTSIDE);
			}
		}
		Stop
	}
}

actor NukerPelletFlash : NukerMissileFlash
{
    Scale 0.0075
    Alpha 0.8
}

actor NukerPelletExploder
{
    +NOINTERACTION
    States
    {
    Spawn:
		TNT1 AAA 1 NoDelay A_SpawnItemEx("NukerPelletExp",frandom(3.0,-3.0),
			frandom(3.0,-3.0),frandom(3.0,-3.0),0,0,0,0,SXF_CLIENTSIDE)
		Stop
    }
}

actor NukerPelletExp : NukerMissileExp
{
	Scale 0.175
}

actor NukerPelletSmoker
{
    +NOINTERACTION
	var int user_counter;
    States
    {
    Spawn:
		TNT1 A 7 {
			for(user_counter = 0; user_counter < 5; user_counter++) {
				A_SpawnItemEx("NukerMissileExpSmoke",frandom(6.0,-6.0),
					frandom(6.0,-6.0),frandom(6.0,-6.0),frandom(0.2,0.4),0,
					frandom(-0.3,0.3),frandom(0.0,360.0),SXF_CLIENTSIDE);
				}
		}
		TNT1 AAA 1 A_SpawnItemEx("NukerMissileExpSmoke",frandom(6.0,-6.0),
			frandom(6.0,-6.0),frandom(6.0,-6.0),frandom(0.2,0.4),0,
			frandom(-0.3,0.3),random(0.0,360.0),SXF_CLIENTSIDE)
		Stop
	}
}

actor NukerPelletExpSmoke : NukerMissileExpSmoke
{
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_SetScale(frandom(0.3,0.2))
		Goto TwatRoll
	}
}

//Deathhead
ACTOR DeathHeadExplode
{ 
    +NOINTERACTION
    States
    {
    Spawn:
		TNT1 A 0 NoDelay A_QuakeEx(7, 7, 7, 25, 0, 512, "none", QF_SCALEDOWN)
		TNT1 AAAA 0 A_SpawnItemEx("DeathHeadExploder",
			frandom(2.0,-2.0),frandom(2.0,-2.0),frandom(2.0,-2.0),
			frandom(1.0,-1.0),frandom(1.0,-1.0),frandom(1.0,-1.0),
			frandom(0.0,360.0),SXF_TRANSFERPOINTERS,0)
		TNT1 A 0 A_JumpIf(GetCVar("RO_SmokeChoker"),"AltBlast")
		TNT1 AAA 0 A_SpawnItemEx("DeathHeadSmoke",
			frandom(50.0, -50.0),frandom(50.0, -50.0),frandom(50.0, -50.0),
			frandom(0.3,1.0), frandom(0.3,1.0), frandom(frandom(0.3,1.0),frandom(-0.3,-1.0)),
			frandom(0.0,360.0),SXF_CLIENTSIDE,0)
		TNT1 A 5
		TNT1 A 0 A_JumpIf(GetCVar("RO_SmokeChoker"),"Null")
		TNT1 A 0 A_SpawnItemEx("DeathHeadAfterSmoke",0,0,0,0,0,0,0,SXF_CLIENTSIDE,0)
		TNT1 AAAAAAA 6 A_SpawnItemEx("DeathHeadAfterSmoke",
			frandom(30.0,-30.0),frandom(30.0,-30.0),frandom(30.0,-30.0),
			frandom(0.3,1.0),frandom(0.3,1.0),frandom(0.3,1.0),
			frandom(0.0,360.0),SXF_CLIENTSIDE,0)
		Stop
	AltBlast:
		TNT1 A 47
		Stop
    }
}

ACTOR DeathHeadShock
{
    Scale 1.5
    Renderstyle "Add"
    +NOINTERACTION
    States
    {
    Spawn:
      BIGB ABCDEFGHIJKLMNOPQRSTU 2 BRIGHT
      Stop
    }
}

ACTOR DeathHeadSmoke : MissileExpSmoke
{ 
    Scale 1.25
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_SetScale(frandom(1.1,1.4))
		Goto Super::Twatroll
	}
}

ACTOR DeathHeadExploder
{ 
    +NOINTERACTION
    +FORCERADIUSDMG
    ReactionTime 10
    States
    {
    Spawn:
		TNT1 A 2 NoDelay {
			A_Explode(25,192,0);
			A_SpawnItemEx("DeathHeadExp",
			frandom(28.0,-28.0),frandom(28.0,-28.0),frandom(28.0,-28.0),
			0,0,0,0,SXF_CLIENTSIDE,0);
			A_Countdown;
		}
		Loop
    Death:
		TNT1 A 0
		Stop
    }
}

ACTOR DeathHeadExp
{
	+NOINTERACTION
	Renderstyle "Add"
	Scale 3.25
	states
	{
	Spawn:
		TNT1 A 0 NoDelay A_Jump(256,"Exp1","Exp2")
	Exp1:
		EXP1 ABCDEFGHIJKLMNOP 1 Bright
		Stop
	Exp2:
		EXP2 ABCDEFGHIJKLMNOP 1 Bright
		Stop
	}
}

ACTOR DeatheadAfterboom : DeathHeadSmoke
{ 
    Scale 1.0
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_SetScale(frandom(1.1,0.9))
		Goto Super::Twatroll
	}
}

ACTOR DeathHeadAfterSmoke
{ 
    +NOINTERACTION
    States
    {
    Spawn:
		TNT1 AAAAAA 5 A_SpawnItemEx("DeathHeadSmoke",
			frandom(25.0,-25.0),frandom(25.0,-25.0),frandom(25.0,-25.0),
			frandom(0.3,1.0),frandom(0.3,1.0),frandom(0.3,1.0),
			frandom(0,360),SXF_CLIENTSIDE,0)
		Stop
    }
}

ACTOR DeathHeadMissileExplode
{ 
    +NOINTERACTION
    States
    {
    Spawn:
		TNT1 A 0
		TNT1 AA 0 A_SpawnItemEx("DeathHeadMissileExploder",
			frandom(9.0,-9.0),frandom(9.0,-9.0),frandom(9.0,-9.0),
			0,0,0,0,SXF_TRANSFERPOINTERS,0)
		Stop
	}
}

ACTOR DeathHeadMissileExploder
{ 
	+NOINTERACTION
	States
    {
	Spawn:
		TNT1 A 13
		TNT1 AAAA 2 A_SpawnItemEx("DeathHeadMissileExp",
			frandom(40.0,-40.0),frandom(40.0,-40.0),frandom(40.0,-40.0),
			0,0,0,0,SXF_TRANSFERPOINTERS,0)
		Stop
	}
}

ACTOR DeathHeadMissileExp
{
	+NOINTERACTION
	Renderstyle "Add"
	Scale 2.45
	states
	{
	Spawn:
		TNT1 A 0 A_Jump(255,"Exp1","Exp2","Exp3","Exp4","Exp5")
		stop
	Exp1:
		EXP6 A 0
		Goto Main
	Exp2:
		EXP7 A 0
		Goto Main
	Exp3:
		EXP8 A 0
		Goto Main
	Exp4:
		EXP9 A 0
		Goto Main
	Exp5:
		EXP0 A 0
		Goto Main
	Main:
		"####" "#" 2 Bright A_Explode(5,128)
		stop
	}
}

//Pushka

ACTOR PushkaExplode
{ 
    +NOINTERACTION
    +NOGRAVITY
    States
    {
    Spawn:
		TNT1 A 0 NoDelay A_QuakeEx(5, 5, 5, 18, 0, 328, "none", QF_SCALEDOWN)
		TNT1 AAAA 0 A_SpawnItemEx("PushkaExploder",
			frandom(4.0,-4.0),frandom(4.0,-4.0),frandom(4.0,-4.0),
			frandom(2.0,-2.0),frandom(2.0,-2.0),frandom(2.0,-2.0),
			frandom(0.0,360.0),SXF_TRANSFERPOINTERS,0)
		TNT1 A 0 A_JumpIf(GetCVar("RO_SmokeChoker"),"AltBlast")
		TNT1 AA 0 A_SpawnItemEx("DeathHeadSmoke",
			frandom(35.0,-35.0),frandom(35.0,-35.0),frandom(35.0,-35.0),
			frandom(0.3,1.0),frandom(0.3,1.0),frandom(0.3,1.0),
			frandom(0,360),SXF_CLIENTSIDE,0)
		TNT1 A 2
		TNT1 A 0 A_SpawnItemEx("DeathHeadAfterSmoke",0,0,0,0,0,0,0,SXF_CLIENTSIDE,0)
		TNT1 AAAAAAA 6 A_SpawnItemEx("DeatheadAfterboom",
			frandom(20.0,-20.0),frandom(20.0,-20.0),frandom(20.0,-20.0),
			frandom(0.3,1.0),frandom(0.3,1.0),frandom(0.3,1.0),
			frandom(0.0,360.0),SXF_CLIENTSIDE,0)
		Stop
	AltBlast:
		TNT1 A 44
		Stop
    }
}

ACTOR PushkaExploder : DeathHeadExploder
{ 
	+NOINTERACTION
    +FORCERADIUSDMG
    ReactionTime 5
    States
    {
    Spawn:
		TNT1 A 0 NoDelay A_Countdown
		TNT1 A 0 A_Explode(25,192,0)
		TNT1 A 2 A_SpawnItemEx("DeathHeadExp",
			frandom(28.0,-28.0),frandom(28.0,-28.0),frandom(28.0,-28.0),
			0,0,0,0,SXF_CLIENTSIDE,0)
		Loop
    Death:
		TNT1 A 1
		Stop
    }
}

ACTOR PushkaExplodeSmall : PushkaExplode
{ 
    +NOINTERACTION
    States
    {
    Spawn:
		TNT1 AAA 0 A_SpawnItemEx("PushkaExploder",
			frandom(4.0,-4.0),frandom(4.0,-4.0),frandom(4.0,-4.0),
			frandom(1.0,-1.0),frandom(1.0,-1.0),frandom(1.0,-1.0),
			frandom(0.0,360.0), SXF_CLIENTSIDE, 0)
		TNT1 A 0 A_JumpIf(GetCVar("RO_SmokeChoker"),"AltBlast")
		TNT1 A 0 A_SpawnItemEx("DeathHeadSmoke",
			frandom(16.0,-16.0),frandom(16.0,-16.0),frandom(16.0,-16.0),
			frandom(0.3,1.0),frandom(0.3,1.0),frandom(0.3,1.0),
			frandom(0.0,360.0),SXF_CLIENTSIDE,0)
		TNT1 A 1
		TNT1 A 0 A_JumpIf(GetCVar("RO_SmokeChoker"),"NULL")
		TNT1 A 0 A_SpawnItemEx("DeathHeadAfterSmoke",0,0,0,0,0,0,0,SXF_CLIENTSIDE,0)
		TNT1 AAAA 1 A_SpawnItemEx("DeatheadAfterboom",
			frandom(5.0,-5.0),frandom(5.0,-5.0),frandom(5.0,-5.0),
			frandom(0.3,1.0),frandom(0.3,1.0),frandom(0.3,1.0),
			frandom(0.0,360.0),SXF_CLIENTSIDE,0)
		Stop
    }
}

//Pushka
actor NukerPushkaExplode
{ 
    +NOINTERACTION
    States
    {
    Spawn:
      TNT1 A 18 NoDelay {
			A_QuakeEx(6, 6, 6, 18, 0, 384, "none", QF_SCALEDOWN);
			A_SpawnItemEx("NukerPushkaFlash",0,0,0,0,0,0,0,SXF_CLIENTSIDE,0);
			A_SpawnItemEx("NukerPushkaExploder",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS,0);
			If(!GetCVar("RO_SmokeChoker")) {
				A_SpawnItemEx("NukerPushkaSmoker",0,0,0,0,0,0,0,SXF_CLIENTSIDE,0);
			}
		}
		Stop
	}
}

actor NukerPushkaFlash : NukerMissileFlash
{
    Scale 0.75
}

actor NukerPushkaExploder
{
	Obituary "%o got caught in high-caliber asplodination."
    ReactionTime 14
    +NOINTERACTION
    +FORCERADIUSDMG
    States
    {
    Spawn:
		TNT1 A 2 NoDelay {
			A_Explode(50,224,0);
			A_SpawnItemEx("NukerPushkaExp",frandom(41.0,-41.0),frandom(41.0,-41.0),
				frandom(41.0,-41.0),0,0,0,0,SXF_TRANSFERPOINTERS);
			A_Countdown;
		}
		Loop
    Death:
		TNT1 A 0
		Stop
    }
}

actor NukerPushkaExploder2 : NukerPushkaExploder
{
    DamageType "Nope"
}

actor NukerPushkaExp : NukerMissileExp
{
	Scale 0.6
}

actor NukerPushkaSmoker
{
    +NOINTERACTION
	var int user_counter;
    States
    {
    Spawn:
		TNT1 A 12
		TNT1 A 6 {
			for(user_counter = 0; user_counter < 10; user_counter++)
			{
				A_SpawnItemEx("NukerPushkaExpSmoke",frandom(56.0,-56.0),
					frandom(56.0,-56.0),frandom(56.0,-56.0),frandom(0.2,0.4),
					frandom(0.2,0.4),frandom(-0.3,0.3),frandom(0.0,360.0),
					SXF_CLIENTSIDE);
			}
		}
		TNT1 AAAAA 2 A_SpawnItemEx("NukerPushkaExpSmoker2",frandom(22.0,-22.0),
			frandom(22.0,-22.0),frandom(22.0,-22.0),0,0,0,0,SXF_CLIENTSIDE)
		Stop
    }
}

actor NukerPushkaExpSmoker2
{
    +NOINTERACTION
    States
    {
    Spawn:
		TNT1 AAAA 2 NoDelay A_SpawnItemEx("NukerPushkaExpSmoke",frandom(39.0,-39.0),
			frandom(39.0,-39.0),frandom(39.0,-39.0),frandom(0.2,0.4),
			frandom(0.2,0.4),frandom(-0.3,0.3),frandom(0.0,360.0),SXF_CLIENTSIDE)
		Stop
    }
}

actor NukerPushkaExpSmoke : NukerMissileExpSmoke
{
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_SetScale(frandom(1.3,1.2))
		Goto TwatRoll
	}
}

//Deathhead (Is that even used anymore?... I shouldn't risk and just retool it.
actor NukerDeathheadExplode
{ 
    +NOINTERACTION
    States
    {
    Spawn:
		TNT1 A 25 NoDelay {
			A_QuakeEx(8, 8, 8, 25, 0, 768, "none", QF_SCALEDOWN);
			A_SpawnItemEx("NukerPushkaFlash",0,0,0,0,0,0,0,SXF_CLIENTSIDE);
			A_SpawnItemEx("NukerPushkaExploder",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS);
			If(!GetCVar("RO_SmokeChoker")) {
				A_SpawnItemEx("NukerPushkaSmoker",0,0,0,0,0,0,0,SXF_CLIENTSIDE);
			}
		}
		Stop
	}
}

Actor OverwhelmingTracer : FastProjectile
{
	Radius 11
	Height 8
	Speed 250
	Damage 8
	Projectile
	+RIPPER
	+THRUGHOST
	+EXPLODEONWATER
	+SEEKERMISSILE
	+DONTSEEKINVISIBLE
	+SCREENSEEKER
	+MTHRUSPECIES
	Renderstyle "Add"
	Decal "BigBulletScorch"
	DeathSound "weapons/larpaexp"
	Obituary "%o was power overwhelmed by %k."
	MissileType "OverwhelmingTrailer"
	MissileHeight 8
	Scale 0.1
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_ChangeVelocity (frandom(-2.5, 2.5), frandom(-2.5, 2.5), frandom(-1.5, 1.5), CVF_RELATIVE)
		TNT1 A 0 A_JumpIfInTargetInventory("OverMode",1,"HomerMode")
		10BA A 1 Bright
		loop
	HomerMode:
		10BA A 1 Bright A_SeekerMissile(11,16,SMF_LOOK|SMF_PRECISE,192,6)
		Goto Spawn
	Death:
		TNT1 A 0 A_JumpIfInTargetInventory("NukerUpgrade",1,"NukerDeath")
		TNT1 A 0 A_Explode(20,64,0)
		TNT1 A 0 A_SpawnItemEx("PelletExplode",0,0,0,0,0,0,0,SXF_CLIENTSIDE,0)
		TNT1 BCD 4
		stop
	NukerDeath:
		TNT1 A 0 A_Explode(40,96,0)
		TNT1 A 0 A_SpawnItemEx("NukerPelletExplode",0,0,0,0,0,0,0,SXF_CLIENTSIDE,0)
		TNT1 BCD 4
		stop
	}
}

Actor CaliberOverwhelmingTracer : OverwhelmingTracer
{
	Damage 14
	States
	{
	Death:
		TNT1 A 0 A_SpawnItemEx("OverwhelmingShockwave",0,0,0,0,0,0,0,SXF_CLIENTSIDE,0)
		Goto Super::Death
	}
}

Actor OverwhelmingShockwave
{
	+NOINTERACTION
	+NOTONAUTOMAP
	Renderstyle Add
	Scale 0.15
	Alpha 0.99
	States
	{
	Spawn:
		UNHS A 1 NoDelay Bright {
			A_SetScale(ScaleX * frandom(1.05,1.25));
			A_FadeOut(0.1);
		}
		Loop
	}
}

Actor OverwhelmingTrailer
{
	+NOINTERACTION
	+NOGRAVITY
	+NOTONAUTOMAP
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_SpawnItemEx("OverwhelmingTrail",
			frandom(1.5,-1.5),frandom(1.5,-1.5),frandom(1.5,-1.5),
			0,0,frandom(1.5,0.0),frandom(0.0,360.0),SXF_CLIENTSIDE,0)
		Stop
	}
}

Actor OverwhelmingTrail
{
	Scale 0.2
	Alpha 0.1
	+NOINTERACTION
	+NOGRAVITY
	Renderstyle Translucent
	States
	{
	Spawn:
		SMKE A 0 NoDelay A_Jump(256,"SmokeA","SmokeB","SmokeC","SmokeD","SmokeE","SmokeF","SmokeG","SmokeH")
	SmokeA:
		SMKE A 0
		Goto Main
	SmokeB:
		SMKE B 0
		Goto Main
	SmokeC:
		SMKE C 0
		Goto Main
	SmokeD:
		SMKE D 0
		Goto Main
	SmokeE:
		SMKE E 0
		Goto Main
	SmokeF:
		SMKE F 0
		Goto Main
	SmokeG:
		SMKE G 0
		Goto Main
	SmokeH:
		SMKE H 0
		Goto Main
	Main:
		"####" "#" 1 A_FadeOut(0.02)
		Loop
	}
}

//Alt-Fire
Actor TankShellBall
{
	Radius 10
	Height 8
	MissileHeight 8
	Speed 70
	Damage 300
	Projectile
	-NOGRAVITY
	+THRUGHOST
	+DONTSPLASH
	+EXPLODEONWATER
	+ROLLSPRITE
	+ROLLCENTER
	+MTHRUSPECIES
	Gravity 0.6
	DeathSound "Weapons/PushkaExp"
	Decal "CannonScorch"
	Renderstyle "Add"
	Obituary "%o was command and conquered by %k's flaming ball of steel."
	var float User_Rotation;
	var float User_Direction;
	Scale 0.5
	States
	{
	Spawn:
		10BA A 0 NoDelay {
			A_ChangeVelocity(0.0,0.0,frandom(1.0,1.5),CVF_RELATIVE);
			A_PlaySound("Weapons/PushkaLoop",4,1.0,1);
			A_SetRoll(frandom(0.0,360.0), SPF_INTERPOLATE);
			User_Rotation == frandom(25.0,35.0);
			User_Direction == frandom(1.0,-1.0);
		}
	Main:
		10BA A 1 Bright {
			A_SetRoll(Roll + (User_Rotation * User_Direction), SPF_INTERPOLATE);
			A_SpawnItemEx("TankShellTrail",
				frandom(-1.0,1.0),frandom(-1.0,1.0),frandom(-1.0,1.0),
				0,0,0,0,SXF_CLIENTSIDE,0);
			A_SpawnItemEx("TankBallTrail",-24,0,0,0,0,0,0,
				SXF_CLIENTSIDE|SXF_TRANSFERSPRITEFRAME|SXF_TRANSFERSCALE|SXF_TRANSFERROLL,0);
		}
		Loop
	Death:
		TNT1 A 0 A_JumpIfInTargetInventory("NukerUpgrade",1,"NukerDeath")
		TNT1 B 0 A_Explode(100,164)
		TNT1 A 0 A_SpawnItemEx("PushkaExplode",0,0,0,0,0,0,0,128,0)
		TNT1 A 0 A_Explode(100,256,0)
		TNT1 BCD 6
		stop
	NukerDeath:
		TNT1 B 0 A_Explode(200,192)
		TNT1 A 0 A_SpawnItemEx("NukerPushkaExplode",0,0,0,0,0,0,0,128,0)
		TNT1 A 0 A_Explode(200,384,0)
		TNT1 BCD 6
		stop
	}
}

Actor TankBallTrail
{
	+NOINTERACTION
	+NOTONAUTOMAP
	Renderstyle Add
	States
	{
	Spawn:
		"####" "#" 1 Bright NoDelay {
			A_SetScale(ScaleX * 0.85);
			A_FadeOut(0.1);
			if(ScaleX <= 0.0)
			{
				Return State("Null");
			}
			Return State("");
		}
		Loop
	}
}

//Dispenser
Actor OverwhelmingDispenserSpawner
{
	const float pitch_deviation = 0; // kd: well it was 0 in Pitchfork Simulation so um...
	Radius 20
	Height 16
	Speed 20
	Damage 200 //Kill someone by deploying dispenser on his face. Lovely!
	Gravity 1.25
	Projectile
	-NOGRAVITY
	+SKYEXPLODE //In case someone will get a genius idea to do that with a low-ceiling sky.
	+MTHRUSPECIES
	States
	{
	Spawn:
		DISD A 0 NoDelay A_SetPitch(
			Frandom(max(-90, pitch - abs(pitch_deviation) ), min(90, pitch + abs(pitch_deviation) )),
			SPF_FORCECLAMP | SPF_INTERPOLATE)
		DISD A 0 A_ChangeVelocity(cos(angle) * cos(pitch) * 20.0, sin(angle) * cos(pitch) * 20.0, (sin(pitch) * 20.0) + 3.0, CVF_REPLACE)
	Main:
		DISD A 1
		Loop
	XDeath:
		DISD A 0 A_PlaySound("Weapons/SatchelHit")
	Death:
		DISD A 0 A_PlaySound("Dispenser/Hit",5)
		DISD AAAAAA 1 A_SetScale(ScaleX + 0.025, ScaleY - 0.1)
		DISD AAAAAA 1 A_SetScale(ScaleX - 0.025, ScaleY + 0.15)
		DISD AAAAAA 1 A_SetScale(ScaleX, ScaleY - 0.05)
		DISD A 0 A_PlaySound("Dispenser/Steam",6)
		TNT1 A 0 A_SpawnItemEx("OverwhelmingDispenser",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS|SXF_TRANSFERTRANSLATION)
		Stop
	}
}

const int Dispenser_Ammo_Ratio = 140; //Time that takes the dispenser ammo charging.
const int Dispenser_Healing_Ratio = 2; //Health recharge rate.
const int Dispenser_Debris = 20; //Amount of junk produced by destroyed dispenser.

actor IsDispensing : Inventory { Inventory.MaxAmount 1 } //For Joe.

Actor OverwhelmingDispenser
{
	Radius 20
	Height 45
	Gravity 1.5
	Species "Vodka"
	+MISSILE
	-NOGRAVITY
	-NOBLOCKMAP
	+NOEXPLODEFLOOR
	+SOLID
	+THRUSPECIES
	+FORCERADIUSDMG
	+DONTBLAST
	//var int User_AmmoCharge; //Changed it to inventory tokens, so it can be preserved between dispensers.
	var int User_CasingCounter;
	var int User_Dispensing;
	var int User_ScrapMetal;
	States
	{
	Spawn:
		DISD A 0 NoDelay {
			//User_AmmoCharge == 0;
			User_CasingCounter == 0;
			User_Dispensing == 0;
			User_ScrapMetal == 0;
			A_PlaySound("Dispenser/Loop",5,1.0,1);
			A_PlaySound("Dispenser/Idle",6,0.4,1);
		}
		DISD ABCD 2
	Active:
		DISP AABBCCDDEEFFGGHHIIJJKK 1 {
			//Do we have casings/any players around to make spenser active?
			If(CountInv("BooletCasing",AAPTR_TARGET) && !A_CheckRange(384,"Null") && !A_CheckSight("Null"))
			{
				/*if(random(0,1) == 1) { 
					if(!CountInv("IsDispensing")) { A_SpawnItemEx("CottonEyedDispenser",0,0,0,0,0,0,0,SXF_ORIGINATOR|SXF_SETMASTER); }
					A_GiveInventory("IsDispensing",1); //So Joe knows what's up.
					A_PlaySound("Dispenser/CottonJoe",2,1.0,1);
				}*/
			
				//Did spenser stop... dispensing?
				If(User_Dispensing == 0)
				{
					A_SetUserVar("User_Dispensing",1); //Restart le spenser.
					A_PlaySound("Dispenser/Start",4);
					A_PlaySound("Dispenser/Loop",5,1.0,1);
				}
				
				//Counters for ammo and heal dispensing.
				A_SetUserVar("User_CasingCounter", User_CasingCounter + 1);
				A_GiveInventory("DispenserCharge",1,AAPTR_TARGET);
				//A_SetUserVar("User_AmmoCharge", User_AmmoCharge + 1);
				
				//Healing aura to indicate that thing is active.
				A_SpawnItemEx("DispenserHealingEffect",0,0,0,0,0,0,0,SXF_CLIENTSIDE);

				//Healing.
				if(User_CasingCounter >= Dispenser_Healing_Ratio)
				{
					A_RadiusGive("DispenserHealing",384,RGF_PLAYERS,1);
					A_Explode(5,384,0);
					A_TakeInventory("BooletCasing",1,TIF_NOTAKEINFINITE,AAPTR_TARGET);
					A_SetUserVar("User_CasingCounter",0);
				}
				
				//Ammo.
				//if(User_AmmoCharge >= 175)
				if(CountInv("DispenserCharge",AAPTR_TARGET) >= Dispenser_Ammo_Ratio)
				{
					A_PlaySound("Dispenser/Ammo",7);
					//Set it as master, so it can do the jumpy-stuff while pulling out ammo.
					//The player is still the target, and this is a projectile, so it should be all goodie.
					A_SpawnItemEx("AmmoDispense",0,0,0,0,0,0,0,SXF_ORIGINATOR|SXF_SETMASTER);
					A_TakeInventory("DispenserCharge",999,0,AAPTR_TARGET);
					//A_SetUserVar("User_AmmoCharge",0);
				}
			}
			Else
			{
				//OH NO WE HAD RAN OUT OF JUICE.
				A_SetUserVar("User_Dispensing",0);
				A_TakeInventory("IsDispensing",1);
				If(A_CheckFloor("Null")) { A_ChangeVelocity(0,0,0,CVF_REPLACE); }
				A_StopSound(5);
				A_StopSound(2);
			}
			
			//Engineer: Hurrrrr.
			If(CountInv("DispenserDetonate", AAPTR_TARGET))
			{
				Return State("Detonate");
			}
			Return State("");
		}
		Loop
	Detonate:
		TNT1 A 0 {
			A_TakeInventory("DispenserOut",1,0,AAPTR_TARGET);
			A_PlaySound("Dispenser/Death",5);
			A_StopSound(6);
			A_NoBlocking;
			//Was about to just repeat some frames here for desired effect,
			//but didn't give in to the laziness. SUCCESS!
			for(A_SetUserVar(User_ScrapMetal, 0); User_ScrapMetal < Dispenser_Debris; A_SetUserVar(User_ScrapMetal, User_ScrapMetal + 1))
			{
				A_SpawnItemEx("BarrelParts",
					frandom(5.0,-5.0),frandom(5.0,-5.0),frandom(0.5,10.0),
					frandom(2.0,8.0),0,random(6.0,12.0),random(0,360),
					SXF_CLIENTSIDE,20);
			}
		} 
		TNT1 A 0 A_JumpIf(CountInv("NukerUpgrade",AAPTR_TARGET),"NukerDeath")

		TNT1 B 0 A_Explode(150,164,0) //Dope.
		TNT1 A 0 A_Explode(150,256,0)
		TNT1 A 0 A_SpawnItemEx("PushkaExplode",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS,0)
		Goto DISPENSERDOWN
	NukerDeath:
		TNT1 B 0 A_Explode(300,192,0)
		TNT1 A 0 A_Explode(300,384,0)
		TNT1 A 0 A_SpawnItemEx("NukerPushkaExplode",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS,0)
		Goto DISPENSERDOWN
	DISPENSERDOWN:
		TNT1 BCD 6 //Used for GLDEFS, explosion stuff.
		stop
	}
}


//Heal item
ACTOR DispenserHealing : Health
{
	Inventory.PickupSound ""
	Inventory.PickupMessage ""
	Inventory.Amount 4
	Inventory.MaxAmount 999999
	+INVENTORY.ALWAYSPICKUP
	-COUNTITEM
	-INVENTORY.PICKUPFLASH
	States
	{
	Spawn:
		TNT1 A -1
		Stop
	}
}

Actor AmmoDispense
{
	+NOINTERACTION
	+NOTONAUTOMAP
	+DONTBLAST
	States
	{
	Spawn:
		TNT1 A 1 NoDelay A_SetScale(1.025,0.9, AAPTR_MASTER)
		TNT1 A 1 A_SetScale(1.05,0.8, AAPTR_MASTER)
		TNT1 A 1 A_SetScale(1.075,0.7, AAPTR_MASTER)
		TNT1 A 1 A_SetScale(1.1,0.6, AAPTR_MASTER)
		TNT1 A 1 A_SetScale(1.125,0.5, AAPTR_MASTER)
		TNT1 A 1 A_SetScale(1.15,0.4, AAPTR_MASTER)
		
		TNT1 A 1 A_SetScale(1.125,0.55, AAPTR_MASTER)
		TNT1 A 1 A_SetScale(1.1,0.7, AAPTR_MASTER)
		TNT1 A 1 A_SetScale(1.075,0.85, AAPTR_MASTER)
		TNT1 A 1 A_SetScale(1.05,1.0, AAPTR_MASTER)
		TNT1 A 1 A_SetScale(1.025,1.15, AAPTR_MASTER)
		TNT1 A 1 A_SetScale(1.0,1.30, AAPTR_MASTER)
		
		TNT1 A 0 A_SpawnItemEx("Backpack")
		
		TNT1 A 1 A_SetScale(1.0,1.25, AAPTR_MASTER)
		TNT1 A 1 A_SetScale(1.0,1.2, AAPTR_MASTER)
		TNT1 A 1 A_SetScale(1.0,1.15, AAPTR_MASTER)
		TNT1 A 1 A_SetScale(1.0,1.1, AAPTR_MASTER)
		TNT1 A 1 A_SetScale(1.0,1.05, AAPTR_MASTER)
		TNT1 A 1 A_SetScale(1.0,1.0, AAPTR_MASTER)
		Stop
		
		/* For some reason, retrieving the value doesn't work well. So! I have to type in the changes per frame. Amazeballs.
		TNT1 AAAAAA 1 A_SetScale(ScaleX + 0.025, ScaleY - 0.1, AAPTR_MASTER)
		TNT1 AAAAAA 1 A_SetScale(ScaleX - 0.025, ScaleY + 0.15, AAPTR_MASTER)
		TNT1 A 0 A_SpawnItemEx("AmmoPinata")
		TNT1 AAAAAA 1 A_SetScale(ScaleX, ScaleY - 0.05, AAPTR_MASTER)
		Stop*/
	}
}

Actor CottonEyedDispenser : AmmoDispense
{
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_JumpIf(!CountInv("IsDispensing",AAPTR_MASTER),"Null")
		TNT1 A 1 A_SetScale(1.025,0.9, AAPTR_MASTER)
		TNT1 A 1 A_SetScale(1.075,0.7, AAPTR_MASTER)
		TNT1 A 1 A_SetScale(1.1,0.6, AAPTR_MASTER)
		TNT1 A 1 A_SetScale(1.15,0.4, AAPTR_MASTER)
		
		TNT1 A 1 A_SetScale(1.125,0.55, AAPTR_MASTER)
		TNT1 A 1 A_SetScale(1.05,1.0, AAPTR_MASTER)
		TNT1 A 1 A_SetScale(1.0,1.30, AAPTR_MASTER)
		TNT1 A 0 A_ChangeVelocity(frandom(2.0,-2.0),frandom(2.0,-2.0),6.0,CVF_RELATIVE,AAPTR_MASTER)
		
		TNT1 A 1 A_SetScale(1.0,1.25, AAPTR_MASTER)
		TNT1 A 1 A_SetScale(1.0,1.15, AAPTR_MASTER)
		TNT1 A 1 A_SetScale(1.0,1.0, AAPTR_MASTER)
		TNT1 A 6
		Loop
	}
}

Actor AmmoPinata : AmmoDispense
{
	var int User_Counter;
	States
	{
	Spawn:
		TNT1 A 0 NoDelay {
			for(A_SetUserVar(User_Counter, 0); User_Counter < 4; A_SetUserVar(User_Counter, User_Counter + 1))
			{
				A_SpawnItemEx("Clip",0,0,0,
					frandom(7.0,2.0),frandom(7.0,2.0),frandom(15.0,5.0),
					frandom(0.0,360.0),0,64);
			}
			for(A_SetUserVar(User_Counter, 0); User_Counter < 4; A_SetUserVar(User_Counter, User_Counter + 1))
			{
				A_SpawnItemEx("Shell",0,0,0,
					frandom(7.0,2.0),frandom(7.0,2.0),frandom(15.0,5.0),
					frandom(0.0,360.0),0,64);
			}
			for(A_SetUserVar(User_Counter, 0); User_Counter < 3; A_SetUserVar(User_Counter, User_Counter + 1))
			{
				A_SpawnItemEx("RocketAmmo",0,0,0,
					frandom(7.0,2.0),frandom(7.0,2.0),frandom(15.0,5.0),
					frandom(0.0,360.0),0,128);
			}
			for(A_SetUserVar(User_Counter, 0); User_Counter < 2; A_SetUserVar(User_Counter, User_Counter + 1))
			{
				A_SpawnItemEx("Cell",0,0,0,
					frandom(7.0,2.0),frandom(7.0,2.0),frandom(15.0,5.0),
					frandom(0.0,360.0),0,128);
			}
		}
		Stop
	}
}

Actor DispenserHealingEffect : AmmoPinata
{
	States
	{
	Spawn:
		TNT1 A 0 NoDelay {
			A_SetAngle(frandom(0.0,360.0)); //To make it less... Square'ish.
			for(A_SetUserVar(User_Counter, 0); User_Counter < 12; A_SetUserVar(User_Counter, User_Counter + 1))
			{
				A_SpawnItemEx("DispenserHealingParticle",frandom(240.0,-240.0),frandom(240.0,-240.0),frandom(55.0,-15.0),
					frandom(0.25,-0.25),frandom(0.25,-0.25),frandom(1.0,0.25),
					frandom(0.0,360.0),0,128);
			}
		}
		//Wanker wanker wanker wanker wanker wanker wanker~
		TNT1 AAAAAAAAAAAAAAA 1 A_ChangeVelocity(frandom(0.5,-0.5),frandom(0.5,-0.5),frandom(0.5,-0.5),CVF_RELATIVE)
		Stop
	}
}

ACTOR DispenserHealingParticle : AmmoDispense
{
	Alpha 0.7
	Scale 0.25
	Renderstyle Add
	States
	{
	Spawn:
		MED3 B 1 NoDelay Bright {
			A_FadeOut(frandom(0.025,0.05));
			A_ChangeVelocity(frandom(0.05,-0.05),frandom(0.05,-0.05),frandom(0.2,0.05),CVF_RELATIVE);
		}
		Loop
	}
}
