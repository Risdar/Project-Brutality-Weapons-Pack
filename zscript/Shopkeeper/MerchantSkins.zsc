class HDMerchantSkin
{
	Name SkinId;
	vector2 Scale;
	static HDMerchantSkin Create(Name id, vector2 scl)
	{
		HDMerchantSkin Skin = new('HDMerchantSkin');
		Skin.SkinId = id;
		Skin.Scale = scl;
		return Skin;
	}
}

extend class HDMerchant
{
	private void TryPlaySpecialAnimation()
	{
		if (!SelectedSkin)
		{
			return;
		}

		StateLabel SpecAnimState;
		string Snd;
		switch (SelectedSkin.SkinId)
		{
			case 'RESM': SpecAnimState = 'Buying.ResidentEvil'; Snd = "ResidentEvil/Buying"; break;			case 'WTRD': SpecAnimState = 'Buying.WanderingTrader'; Snd = "WanderingTrader/Buying"; break;
		}

		if (SpecAnimState)
		{
			A_StartSound(Snd, CHAN_VOICE);
			SetStateLabel(SpecAnimState);
		}
	}

	override void BeginPlay()
	{
		Super.BeginPlay();

		static const Name CVarNames[] = { 'hdm_skin_weapon', 'hdm_skin_ammo', 'hdm_skin_item' };
		Array<HDMerchantSkin> AvailableSkins;
		int SelectedSkins = CVar.GetCVar(CVarNames[ItemsType], players[consoleplayer]).GetInt();

		if (SelectedSkins & 1 << 0) { AvailableSkins.Push(HDMerchantSkin.Create('RESM', (1.0, 1.0))); }
		if (SelectedSkins & 1 << 1) { AvailableSkins.Push(HDMerchantSkin.Create('HOBM', (1.0, 1.0))); }
		if (SelectedSkins & 1 << 12) { AvailableSkins.Push(HDMerchantSkin.Create('WTRD', (1.0, 1.0))); }

		if (AvailableSkins.Size() > 0)
		{
			SelectedSkin = AvailableSkins[random(0, AvailableSkins.Size() - 1)];
		}
	}

	States
	{
		RegisterSprites:
			RESM A 0; HOBM A 0;
			SUC1 A 0; SUC2 A 0;
			MSHU A 0;
			WTRD A 0;
			DAI1 A 0; DAI2 A 0; DAI3 A 0; DAI4 A 0; DAI5 A 0; DAI6 A 0; DAI7 A 0;
			NAR1 A 0;
			S1G3 A 0;
			HD2M A 0;
			HD3N A 0;
		Spawn:
			HOBM A 0 NoDelay
			{
				if (SelectedSkin)
				{
					sprite = GetSpriteIndex(SelectedSkin.SkinId);
					scale = SelectedSkin.Scale;
				}
			}
		SpawnLoop:
			#### A 1 CheckBreathing();
			Loop;

		Buying.ResidentEvil:
			#### A 1 BuyingIdle('SpawnLoop', "ResidentEvil/GoAway");
			Loop;

		Buying.WanderingTrader:
			#### A 1 BuyingIdle('SpawnLoop', "WanderingTrader/GoAway");
			Loop;
	}
}